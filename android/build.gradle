buildscript {
    ext {
        kotlin_version = '1.9.22'
        compileSdkVersion = 35
    }
    
    repositories {
        google()
        mavenCentral()
    }
}

// Configuration des plugins pour tous les projets
plugins {
    id 'com.android.application' version '8.2.1' apply false
    id 'com.android.library' version '8.2.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.22' apply false
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    beforeEvaluate { project ->
        project.setProperty('android.useAndroidX', true)
        project.setProperty('android.enableJetifier', true)
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(':app')
    
    if (project.name.startsWith("flutter")) {
        project.configurations.all {
            resolutionStrategy {
                force 'androidx.core:core-ktx:1.12.0'
                force 'androidx.annotation:annotation:1.7.0'
            }
        }
    }
    
    project.plugins.withId('com.android.library') {
        project.android {
            compileSdkVersion rootProject.ext.compileSdkVersion
            
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_21
                targetCompatibility JavaVersion.VERSION_21
            }
        }
    }
    
    project.plugins.withId('org.jetbrains.kotlin.android') {
        project.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = '21'
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
