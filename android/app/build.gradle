plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "com.google.gms.google-services"
    id "com.google.firebase.crashlytics"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "com.kinder2149.paperclip2"
    compileSdkVersion 34
    ndkVersion flutter.ndkVersion

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += [
            '-Xskip-metadata-version-check'
        ]
    }

    defaultConfig {
        applicationId "com.kinder2149.paperclip2"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 3
        versionName "1.0.1"
        multiDexEnabled true
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            
            // Ajouter ces règles pour R8
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.multidex:multidex:2.0.1'
    
    // Firebase
    implementation platform('com.google.firebase:firebase-bom:32.7.2')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    
    // Play Services Games (pour le jeu)
    implementation 'com.google.android.gms:play-services-games-v2:17.0.0'
    
    // Play Core API (nouvelle version)
    implementation 'com.google.android.play:app-update:2.1.0'          // Pour les mises à jour d'app
    implementation 'com.google.android.play:app-update-ktx:2.1.0'      // Extensions Kotlin pour les mises à jour
    implementation 'com.google.android.play:review:2.0.1'             // Pour les avis utilisateurs
    implementation 'com.google.android.play:review-ktx:2.0.1'         // Extensions Kotlin pour les avis
    
    
}